<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:comp="views.components.*" 
	layout="absolute" width="1360" height="768" showStatusBar="false" 
	creationComplete="doCreationComplete(event)" closing="doClosingApplication(event)" xmlns:views="views.*">
	<mx:Script>
		<![CDATA[
			import views.components.H264Video;
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.events.CloseEvent;
			import model.vo.SiteVO;
			import model.KioskModelLocator;
			import util.XMLSettingsUtil;
			
			private function doCreationComplete(event:Event):void {
				XMLSettingsUtil.loadSitesData();
				NativeApplication.nativeApplication.addEventListener(InvokeEvent.INVOKE, onInvoke);
				vMap.layoutPins();
			}
			
			private function doClosingApplication(event:Event):void {
				if (isAdminShowing()){
					promptForSaveAndExit();
					event.stopImmediatePropagation();
					event.stopPropagation();
				}
			}
			
			public function promptForSaveAndExit():void {
				// TODO: Make this work
				if (vAdmin.dataIsDirty){
					Alert.yesLabel = "Save";
					Alert.noLabel = "Discard";
					var result:Alert = Alert.show("Save before exit?", "Confirm Remove", Alert.YES | Alert.NO, this, promptForSaveAndExitHandler);	
				}		
			}
			
			private function promptForSaveAndExitHandler(event:CloseEvent):void {
				if (event.detail == Alert.YES){
					vAdmin.saveFormToDisk();
				} else {
					// Alert.NO
				}
				Application.application.close();
			}
			
			private function onInvoke(event:InvokeEvent):void
			{
				//arguments passed to app are stored as array in event.arguments
				var args:String;
				args = "Invoke : " + event.arguments + "\n";
				var launchAdminInterface:Boolean = false;
				var startFullscreen:Boolean = false;
				for each (var arg:String in event.arguments){
					if (arg.toUpperCase() == "/ADMIN"){
						this.width = 800;
						this.height = 600;
						launchAdminInterface = true;
					} else if (arg.toUpperCase() == "/FULLSCREEN"){
						startFullscreen = true;
					}
				}
				
				if (launchAdminInterface){
					showAdmin();
				} else if (startFullscreen){
					makeFullscreen();
				}
			}
			
			public function showSiteProfile(site:SiteVO):void {
				vwMain.selectedChild = this.vSiteProfile;
				vSiteProfile.site = site;
			}
			
			public function showMap(placePin:Boolean = false, site:SiteVO = null):void {
				if (placePin) {
					makeFullscreen();
				}
				
				vMap.placePin = placePin;
				vMap.siteForPlacedPin = site;
				vwMain.selectedChild = this.vMap;
			}
			
			public function showAdmin():void {
				vwMain.selectedChild = this.vAdmin;
			}
			
			public function returnToAdminFromPinPlacement(pinWasPlaced:Boolean):void {
				vwMain.selectedChild = this.vAdmin;
				
				if (pinWasPlaced) {
					vAdmin.makeDirty();
				}
			}
			
			public function makeFullscreen():void {
				mx.core.Application.application.stage.displayState = StageDisplayState.FULL_SCREEN;
			}
			
			public function makeWindowed():void {
				mx.core.Application.application.stage.displayState = StageDisplayState.NORMAL;
			}
			
			private function isAdminShowing():Boolean {
				return (vwMain.selectedChild == this.vAdmin);
			}
			
		]]>
	</mx:Script>
	<mx:ViewStack id="vwMain" height="100%" width="100%">
		<views:MapVW id="vMap" />
		<views:AdminVW id="vAdmin" />
		<views:SiteProfileVW id="vSiteProfile" />
		<!-- mx:Label text="{SiteVO(KioskModelLocator.getInstance().sites.getItemAt(1)).videoPath}" selectable="true" /-->
		<!-- mx:VideoDisplay id="vid" width="100%" height="100%" source="{SiteVO(KioskModelLocator.getInstance().sites.getItemAt(0)).videoPath}" autoPlay="true" /-->
		<!-- comp:H264Video id="vid1" x="10" y="10" width="100%" height="100%" filePath="C:\Documents and Settings\User\My Documents\WWKiosk\Jim\Batman_Begins.mov"  /-->
		<!-- mx:VideoDisplay id="vid2" width="100%" height="100%" source="{SiteVO(KioskModelLocator.getInstance().sites.getItemAt(1)).videoPath}" autoPlay="true" /-->
		<!-- mx:Button label="Full Screen" click="mx.core.Application.application.stage.displayState = StageDisplayState.FULL_SCREEN;" /-->
		<!-- mx:Button label="Play" click="addVideo(event)" /-->
	</mx:ViewStack>
	
</mx:WindowedApplication>
