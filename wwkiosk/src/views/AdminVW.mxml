<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:components="views.components.*" width="50%" height="80%" 
	creationComplete="doCreationComplete(event)" show="doPrepareForm(event)">
	<mx:Script>
		<![CDATA[
			import model.vo.PeopleVO;
			import mx.core.Application;
			import mx.skins.halo.ButtonSkin;
			import flash.profiler.profile;
			import util.XMLSettingsUtil;
			import model.KioskModelLocator;
			import model.vo.SiteVO;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			
			public var dataIsDirty:Boolean = false;
			
			// Returns the native path to the directory that contains sites.xml and has the site
			// directories as subdirectories.
			private function get mainConfigDirPath():String {
				return File.documentsDirectory.resolvePath("WWKiosk").nativePath;
			}
			
			private function doCreationComplete(event:Event):void {
				XMLSettingsUtil.loadSitesData();
			}
			
			private function doPrepareForm(event:Event):void {
				savebutton.enabled = false;
				cancelbutton.enabled = false;
			}
			
			private function doSaveForm(event:Event):void {
				saveFormToDisk();
				savebutton.enabled = false;
				cancelbutton.enabled = false;
			}
			
			private function doRevertToSavedSettings(event:Event):void {
				var selectedIndex:int = lstSites.selectedIndex;
				
				XMLSettingsUtil.loadSitesData();
				
				if (lstSites.numChildren > selectedIndex){
					lstSites.selectedIndex = selectedIndex;
				} else if (lstSites.numChildren >= 1) {
					lstSites.selectedIndex = 0;
				} else {
					// No sites, so don't select one
				}
				
				savebutton.enabled = false;
				cancelbutton.enabled = false;
			}
			
			private function doAddSite(event:Event):void {
				var newSite:SiteVO = new SiteVO();
				newSite.locationName = "New Site";
				newSite.people = new PeopleVO();
				KioskModelLocator.getInstance().sites.addItem(newSite);
			}
			
			private function doRemoveSite(event:Event):void {
				var result:Alert = Alert.show("Are you sure you want to remove this site?", "Confirm Remove", Alert.OK | Alert.CANCEL, this, confirmRemoveClickHandler);
			}
			
			private function confirmRemoveClickHandler(event:CloseEvent):void {
				if (event.detail == Alert.OK){
					var selectedSite:SiteVO = lstSites.selectedItem as SiteVO;
					if (selectedSite != null){
						var selectedIndex:int = KioskModelLocator.getInstance().sites.getItemIndex(selectedSite);
						KioskModelLocator.getInstance().sites.removeItemAt(selectedIndex);
					}
				}
			}
			
			private function doPlacePinOnMap(event:Event):void {
				mx.controls.Alert.show("Place pin not yet implemented");
			}
			
			public function saveFormToDisk():void {
				saveFormToModel();
				XMLSettingsUtil.writeSiteDataToFile();
			}
			
			private function saveFormToModel():void {
				var selectedSite:SiteVO = lstSites.selectedItem as SiteVO;
				
				selectedSite.locationName = locationnameinput.text;
				selectedSite.sourceDir = subdirectoryinput.text;
				selectedSite.videoPath = videopathinput.text;
				selectedSite.profileImagePath = profilepathinput.text;
				selectedSite.people.story = storyinput.text;
				selectedSite.people.names = nameinput.text;
			}
			
			private function doDataChanged(event:Event):void {
				dataIsDirty = true;
				savebutton.enabled = true;
				cancelbutton.enabled = true;
			}
			
			////////////////
			// File choosers
			////////////////
			
			// Thumbnail Image
			private function doChooseThumbnailImage(event:Event):void {
				var selectedSite:SiteVO = lstSites.selectedItem as SiteVO;
				var sourceDir:String = mainConfigDirPath + File.separator + selectedSite.sourceDir;
				var fileChooser:File = new File(sourceDir);
				var imgFilter:FileFilter = new FileFilter("Image", "*.gif;*.jpg;*.bmp;*.png;");
				
				try {
					fileChooser.browseForOpen("Choose Thumbnail Image", [imgFilter]);
					fileChooser.addEventListener(Event.SELECT, updateThumbnailImagePath);
				} catch (error:Error){
				    trace("Failed:", error.message)
				}
			}
			
			private function updateThumbnailImagePath(event:Event):void {
				var chosenFile:File = event.target as File;
				
				// Update the form
				profilepathinput.text = chosenFile.nativePath;
				
				// Update the model
				var selectedSite:SiteVO = lstSites.selectedItem as SiteVO;
				selectedSite.profileImagePath = chosenFile.nativePath;
			}
			
			// Site subdirectory
			private function doChooseSubdirectory(event:Event):void {
				var sourceDir:String = mainConfigDirPath;
				var fileChooser:File = new File(sourceDir);
				
				try {
					fileChooser.browseForDirectory("Choose Directory");
					fileChooser.addEventListener(Event.SELECT, addSubdirectoryPathToTextInput);
				} catch (error:Error){
				    trace("Failed:", error.message)
				}
			}
			
			private function addSubdirectoryPathToTextInput(event:Event):void {
				var chosenDir:File = event.target as File;
				this.subdirectoryinput.text = chosenDir.name;
			}
			
			// Video File
			private function doChooseVideoFile(event:Event):void {
				var selectedSite:SiteVO = lstSites.selectedItem as SiteVO;
				var sourceDir:String = mainConfigDirPath + File.separator + selectedSite.sourceDir;
				var fileChooser:File = new File(sourceDir);
				var vidFilter:FileFilter = new FileFilter("Video File", "*.mov;*.mp4;*.flv;");
				
				try {
					fileChooser.browseForOpen("Choose Video File", [vidFilter]);
					fileChooser.addEventListener(Event.SELECT, addVideoPathToTextInput);
				} catch (error:Error){
				    trace("Failed:", error.message)
				}
			}
			
			private function addVideoPathToTextInput(event:Event):void {
				var chosenFile:File = event.target as File;
				this.videopathinput.text = chosenFile.nativePath;
			}
		]]>
	</mx:Script>
	<mx:VBox height="100%" width="15%">
		<mx:List id="lstSites" left="0" width="100%" height="100%" bottom="0" top="0" labelField="locationName" dataProvider="{KioskModelLocator.getInstance().sites}" selectedIndex="0" />
		<mx:HBox width="100%" height="30">
			<mx:Button label="Add" click="doAddSite(event)" />
			<mx:Button label="Remove" click="doRemoveSite(event)" />
		</mx:HBox>
	</mx:VBox>
	
	<mx:VBox height="100%" width="85%">
		<mx:Form width="100%" height="100%">
			<mx:FormItem label="Missionaries' Name(s):" width="100%">
				<mx:TextInput id="nameinput" text="{SiteVO(lstSites.selectedItem).people.names}" width="100%" change="doDataChanged(event)" />
			</mx:FormItem>
			
			<mx:FormItem label="Missionaries' Location:" width="100%">
				<mx:TextInput id="locationnameinput" text="{SiteVO(lstSites.selectedItem).locationName}" width="100%" change="doDataChanged(event)" />
			</mx:FormItem>
			
			<mx:FormItem label="Sub-Directory Name:" width="100%">
				<mx:HBox width="100%">
					<mx:TextInput id="subdirectoryinput" text="{SiteVO(lstSites.selectedItem).sourceDir}" width="100%" change="doDataChanged(event)" />
					<mx:Button label="Choose Directory" click="doChooseSubdirectory(event)" />
				</mx:HBox>
			</mx:FormItem>
			
			<mx:FormItem label="Video:" width="100%">
				<mx:HBox width="100%">
					<mx:TextInput id="videopathinput" text="{SiteVO(lstSites.selectedItem).videoPath}" width="100%" change="doDataChanged(event)" />
					<mx:Button label="Choose Video File" click="doChooseVideoFile(event)" />
				</mx:HBox>
			</mx:FormItem>
			
			<mx:FormItem label="Thumbnail Picture:" width="100%" direction="horizontal">
				<mx:HBox width="100%">
					<components:SmoothImage source="{SiteVO(lstSites.selectedItem).profileImagePath}" width="120" height="120" />
					<mx:TextInput id="profilepathinput" text="{SiteVO(lstSites.selectedItem).profileImagePath}" width="100%" change="doDataChanged(event)" />
					<mx:Button label="Choose Image" click="doChooseThumbnailImage(event)" />
				</mx:HBox>
			</mx:FormItem>
			
			<mx:FormItem label="Map Marker:" width="100%">
				<mx:Button label="Choose Location" click="doPlacePinOnMap(event)" />
			</mx:FormItem>
			
			<mx:FormItem label="Story:" width="100%" height="100%">
				<mx:TextArea id="storyinput" text="{SiteVO(lstSites.selectedItem).people.story}" width="100%" height="100%" change="doDataChanged(event)" />
			</mx:FormItem>
		</mx:Form>

		<mx:HBox height="30" width="100%">
			<mx:Spacer width="100%" />
			<mx:Button id="savebutton" label="Save" click="doSaveForm(event)" />
			<mx:Button id="cancelbutton" label="Cancel" click="doRevertToSavedSettings(event)" />
			<mx:Spacer width="10" />
		</mx:HBox>

	</mx:VBox>

</mx:HBox>
